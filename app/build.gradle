plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 0

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "org.mixitconf"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode versionMajor * 100000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "MIXIT_URL", "\"\""
        buildConfigField "String", "EVENT_API", "\"\""
        buildConfigField "String", "USER_API", "\"\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", "MIXIT_URL", "\"https://mixitconf.org/api/\""
            buildConfigField "String", "EVENT_API", "\"2020/\""
            buildConfigField "String", "USER_API", "\"external/\""
        }
        debug {
            // buildConfigField "String", "MIXIT_URL", "\"https://mixitconf-staging.cleverapps.io/api/\""
            buildConfigField "String", "MIXIT_URL", "\"https://mixitconf.org/api/\""
            buildConfigField "String", "EVENT_API", "\"2020/\""
            buildConfigField "String", "USER_API", "\"external/\""
        }
        profile {
            initWith debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation kotlinDependencies.values()
    implementation androidDependencies.values()
    implementation networkDependencies.values()
    implementation thirdPartyDependencies.values()
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
    kapt annotationProcessors.values()

    testImplementation testDependencies.values()
    androidTestImplementation androidTestDependencies.values()
}